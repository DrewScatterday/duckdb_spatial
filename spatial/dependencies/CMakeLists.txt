cmake_minimum_required(VERSION 3.20)

project(dependecies-build)

# Install and build dependencies locally
include(ExternalProject)
set(LOCAL_INSTALL_DIR ${CMAKE_BINARY_DIR}/local)
set_directory_properties(PROPERTIES EP_BASE ${CMAKE_BINARY_DIR}/dependencies)

set(CMAKE_CXX_STANDARD 11)

# Escape semicolons in CMAKE_OSX_ARCHITECTURES before passing to ExternalProject_Add
string(REPLACE ";" "$<SEMICOLON>" CMAKE_OSX_ARCHITECTURES_PACKED "${CMAKE_OSX_ARCHITECTURES}")


set(SQLITE3_LIB_NAME ${CMAKE_STATIC_LIBRARY_PREFIX}sqlite3${CMAKE_STATIC_LIBRARY_SUFFIX})
set(MEMVFS_LIB_NAME ${CMAKE_STATIC_LIBRARY_PREFIX}memvfs${CMAKE_STATIC_LIBRARY_SUFFIX})
set(ZLIB_LIB_NAME ${CMAKE_STATIC_LIBRARY_PREFIX}z${CMAKE_STATIC_LIBRARY_SUFFIX})
set(PROJ_LIB_NAME ${CMAKE_STATIC_LIBRARY_PREFIX}proj${CMAKE_STATIC_LIBRARY_SUFFIX})
set(EXPAT_LIB_NAME ${CMAKE_STATIC_LIBRARY_PREFIX}expat${CMAKE_STATIC_LIBRARY_SUFFIX})
set(GEOS_LIB_NAME ${CMAKE_STATIC_LIBRARY_PREFIX}geos${CMAKE_STATIC_LIBRARY_SUFFIX})
set(GEOS_C_LIB_NAME ${CMAKE_STATIC_LIBRARY_PREFIX}geos_c${CMAKE_STATIC_LIBRARY_SUFFIX})
set(GDAL_LIB_NAME ${CMAKE_STATIC_LIBRARY_PREFIX}gdal${CMAKE_STATIC_LIBRARY_SUFFIX})

if(WIN32)
    # Windows specific names
    set(EXPAT_LIB_NAME libexpatMD.lib)
    set(ZLIB_LIB_NAME zlibstatic.lib)
endif()

# SQLite3
ExternalProject_Add(
    SQLITE3
    URL ${CMAKE_CURRENT_SOURCE_DIR}/third_party/sqlite3
    CONFIGURE_HANDLED_BY_BUILD TRUE
    BUILD_BYPRODUCTS 
        ${LOCAL_INSTALL_DIR}/lib/${SQLITE3_LIB_NAME}
        ${LOCAL_INSTALL_DIR}/lib/${MEMVFS_LIB_NAME}
    CMAKE_ARGS 
    -DCMAKE_INSTALL_PREFIX:PATH=${LOCAL_INSTALL_DIR}
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_OSX_ARCHITECTURES=${CMAKE_OSX_ARCHITECTURES_PACKED}
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON
)

# ZLIB
ExternalProject_Add(
    ZLIB
    URL ${CMAKE_CURRENT_SOURCE_DIR}/third_party/zlib1213.zip
    CONFIGURE_HANDLED_BY_BUILD TRUE
    BUILD_BYPRODUCTS ${LOCAL_INSTALL_DIR}/lib/${ZLIB_LIB_NAME}
    CMAKE_ARGS
    -DCMAKE_INSTALL_PREFIX:PATH=${LOCAL_INSTALL_DIR}
    -DCMAKE_PREFIX_PATH=${LOCAL_INSTALL_DIR}
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_OSX_ARCHITECTURES=${CMAKE_OSX_ARCHITECTURES_PACKED}
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON
    -DBUILD_SHARED_LIBS=OFF
)

# PROJ
ExternalProject_Add(
    PROJ
    DEPENDS SQLITE3
    URL ${CMAKE_CURRENT_SOURCE_DIR}/third_party/proj-9.1.1.zip
    CONFIGURE_HANDLED_BY_BUILD TRUE
    BUILD_BYPRODUCTS ${LOCAL_INSTALL_DIR}/lib/${PROJ_LIB_NAME}
    CMAKE_ARGS
    # CMake options
    -DCMAKE_INSTALL_PREFIX:PATH=${LOCAL_INSTALL_DIR}
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_OSX_ARCHITECTURES=${CMAKE_OSX_ARCHITECTURES_PACKED}
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON
    -DSQLITE3_INCLUDE_DIR=${LOCAL_INSTALL_DIR}/include
    -DSQLITE3_LIBRARY=${LOCAL_INSTALL_DIR}/lib/${SQLITE3_LIB_NAME}
    # PROJ options
    -DBUILD_SHARED_LIBS=OFF
    -DBUILD_APPS=OFF
    -DBUILD_TESTING=OFF
    -DENABLE_CURL=OFF
    -DENABLE_TIFF=OFF
)

# EXPAT
ExternalProject_Add(
    EXPAT
    URL ${CMAKE_CURRENT_SOURCE_DIR}/third_party/expat-2.5.0.tar.bz2
    CONFIGURE_HANDLED_BY_BUILD TRUE
    BUILD_BYPRODUCTS ${LOCAL_INSTALL_DIR}/lib/${EXPAT_LIB_NAME}
    CMAKE_ARGS 
    # CMake options
    -DCMAKE_INSTALL_PREFIX:PATH=${LOCAL_INSTALL_DIR}
    -DCMAKE_PREFIX_PATH=${LOCAL_INSTALL_DIR}
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_OSX_ARCHITECTURES=${CMAKE_OSX_ARCHITECTURES_PACKED}
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON
    # EXPAT options
    -DEXPAT_SHARED_LIBS=OFF
    -DEXPAT_BUILD_TESTS=OFF
    -DEXPAT_BUILD_EXAMPLES=OFF
    -DEXPAT_BUILD_FUZZERS=OFF
    -DEXPAT_BUILD_DOCS=OFF
)



ExternalProject_Add(
    GEOS
    URL ${CMAKE_CURRENT_SOURCE_DIR}/third_party/geos-3.11.2.tar.bz2
    CONFIGURE_HANDLED_BY_BUILD TRUE
    BUILD_BYPRODUCTS 
        ${LOCAL_INSTALL_DIR}/lib/${GEOS_C_LIB_NAME}
        ${LOCAL_INSTALL_DIR}/lib/${GEOS_LIB_NAME}
    CMAKE_ARGS 
    # CMake options
    -DCMAKE_INSTALL_PREFIX:PATH=${LOCAL_INSTALL_DIR}
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_OSX_ARCHITECTURES=${CMAKE_OSX_ARCHITECTURES_PACKED}
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON
    # GEOS options
    -DBUILD_SHARED_LIBS=OFF
    -DBUILD_TESTING=OFF
    -DBUILD_DOCUMENTATION=OFF
    -DBUILD_ASTYLE=OFF
    -DBUILD_GEOSOP=OFF
)

# GDAL
ExternalProject_Add(
    GDAL
    DEPENDS PROJ GEOS EXPAT SQLITE3
    URL ${CMAKE_CURRENT_SOURCE_DIR}/third_party/gdal363.zip
    CONFIGURE_HANDLED_BY_BUILD TRUE
    BUILD_BYPRODUCTS ${LOCAL_INSTALL_DIR}/lib/${GDAL_LIB_NAME}
    CMAKE_ARGS 
    # CMake options
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_OSX_ARCHITECTURES=${CMAKE_OSX_ARCHITECTURES_PACKED}
    -DCMAKE_INSTALL_PREFIX:PATH=${LOCAL_INSTALL_DIR}
    -DCMAKE_PREFIX_PATH=${LOCAL_INSTALL_DIR}
    -DCMAKE_MODULE_PATH=${LOCAL_INSTALL_DIR}/lib/cmake
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON
    -DCMAKE_FIND_ROOT_PATH=${LOCAL_INSTALL_DIR}
    # GDAL Options
    -DGDAL_OBJECT_LIBRARIES_POSITION_INDEPENDENT_CODE=ON # this is needed for GDAL to build with -fPIC
    -DBUILD_TESTING=OFF
    -DBUILD_APPS=OFF
    # Build static library
    -DBUILD_SHARED_LIBS=OFF
    # Disable apps
    -DBUILD_APPS=OFF
    
    # Arrow
    -DGDAL_USE_ARROW=OFF
    -DARROW_USE_STATIC_LIBRARIES=OFF

    # Disable all external drivers unless explicitly enabled
    -DGDAL_USE_EXTERNAL_LIBS=OFF
    -DGDAL_USE_INTERNAL_LIBS=ON

    # Supported drivers
    -DGDAL_USE_GEOS=ON
    -DGDAL_USE_SQLITE3=ON
    -DGDAL_USE_EXPAT=ON
    -DOPENSSL_USE_STATIC_LIBS=FALSE # Propagate to FindOpenSSL.cmake
    
    # This is not true, but a bug in gdal's cmake files
    -DACCEPT_MISSING_SQLITE3_RTREE:BOOL=ON
    -DACCEPT_MISSING_SQLITE3_MUTEX_ALLOC:BOOL=ON

    # remove optional gdal drivers
    -DGDAL_BUILD_OPTIONAL_DRIVERS=OFF
    -DOGR_BUILD_OPTIONAL_DRIVERS=ON

    # Remove bindings
    -DBUILD_PYTHON_BINDINGS=OFF
)


# proj
add_library(proj STATIC IMPORTED)
set_property(TARGET proj PROPERTY IMPORTED_LOCATION ${LOCAL_INSTALL_DIR}/lib/${PROJ_LIB_NAME})
set_property(TARGET proj PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${LOCAL_INSTALL_DIR}/include)

# sqlite3
add_library(sqlite3 STATIC IMPORTED)
set_property(TARGET sqlite3 PROPERTY IMPORTED_LOCATION ${LOCAL_INSTALL_DIR}/lib/${SQLITE3_LIB_NAME})
set_property(TARGET sqlite3 PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${LOCAL_INSTALL_DIR}/include)
set(THREADS_PREFER_PTHREAD_FLAG ON)
find_package(Threads REQUIRED)
set_property(TARGET sqlite3 PROPERTY IMPORTED_LINK_INTERFACE_LIBRARIES Threads::Threads)


# sqlite3_memvfs
add_library(memvfs STATIC IMPORTED)
set_property(TARGET memvfs PROPERTY IMPORTED_LOCATION ${LOCAL_INSTALL_DIR}/lib/${MEMVFS_LIB_NAME})

# zlib
add_library(zlib STATIC IMPORTED)
set_property(TARGET zlib PROPERTY IMPORTED_LOCATION ${LOCAL_INSTALL_DIR}/lib/${ZLIB_LIB_NAME})
set_property(TARGET zlib PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${LOCAL_INSTALL_DIR}/include)

# expat
add_library(expat STATIC IMPORTED)
set_property(TARGET expat PROPERTY IMPORTED_LOCATION ${LOCAL_INSTALL_DIR}/lib/${EXPAT_LIB_NAME})
set_property(TARGET expat PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${LOCAL_INSTALL_DIR}/include)


# geos_c
add_library(geos_c STATIC IMPORTED)
set_property(TARGET geos_c PROPERTY IMPORTED_LOCATION ${LOCAL_INSTALL_DIR}/lib/${GEOS_C_LIB_NAME})
set_property(TARGET geos_c PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${LOCAL_INSTALL_DIR}/include)

# geos
add_library(geos STATIC IMPORTED)
set_property(TARGET geos PROPERTY IMPORTED_LOCATION ${LOCAL_INSTALL_DIR}/lib/${GEOS_LIB_NAME})
set_property(TARGET geos PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${LOCAL_INSTALL_DIR}/include)

# gdal
add_library(gdal STATIC IMPORTED)
set_property(TARGET gdal PROPERTY IMPORTED_LOCATION ${LOCAL_INSTALL_DIR}/lib/${GDAL_LIB_NAME})
set_property(TARGET gdal PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${LOCAL_INSTALL_DIR}/include)
if(WIN32)
    set_property(TARGET gdal PROPERTY IMPORTED_LINK_INTERFACE_LIBRARIES wbemuuid.lib)
endif()

add_library(dependencies INTERFACE)
add_dependencies(dependencies SQLITE3 PROJ ZLIB EXPAT GEOS GDAL)

# Ouch! Remember that the order of these libraries is important! (reverse order of dependencies)
target_link_libraries(dependencies INTERFACE gdal geos_c geos proj expat memvfs sqlite3 zlib)

target_include_directories(dependencies INTERFACE 
    $<BUILD_INTERFACE:${LOCAL_INSTALL_DIR}/include>  
    $<INSTALL_INTERFACE:include>
)